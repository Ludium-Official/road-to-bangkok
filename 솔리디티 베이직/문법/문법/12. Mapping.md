Mapping
=======
~~영어로 처리된 주석 한글로 바꿔주세요~~<br>
mapping은 다른 언어의 해시 테이블 또는 딕셔너리에 해당하는 자료 구조입니다. mapping은 키-값 쌍을 저장하며, 각 키는 유일하고 특정 값에 매핑됩니다.
이 자료 구조는 스마트 컨트랙트에서 데이터를 효율적으로 저장하고 조회하는데 유용합니다.

기본 구문
------
mapping을 선언하는 기본 구문은 다음과 같습니다.
```solidity
mapping(keyType => valueType) public myMapping;
```
여기서 `keyType`은 매핑의 키 타입을, `valueType`은 매핑의 값 타입을 나타냅니다.
예를 들어, `uint`를 키로 하고 `string`을 값으로 하는 매핑은 다음과 같이 선언할 수 있습니다.
```solidity
mapping(uint => string) public myMapping;
```

특징
---
1. 키- 값 저장
- `mapping`은 특정 키와 그 키에 대응하는 값을 저장합니다. 예를 들어 `mapping(address => uint)`는 각 주소에 대해 `uint` 값을 저장할 수 있습니다.

2. 빠른 조회
- 해시 테이블과 유사하게 `mapping`은 키를 이용해 값을 빠르게 조회할 수 있습니다. 이는 스마트 컨트랙트에서 중요한 성능 요소입니다.

3. 디폴트 값
- 매핑에서 존재하지 않는 키를 조회할 경우, `valueType`에 해당하는 디폴트 값이 반환됩니다. 예를 들어 `uint` 타입의 값이 저장되지 않은 키를 조회하면 0이 반환됩니다.

예제 코드
------
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract Mapping {
    // Mapping from address to uint
    mapping(address => uint256) public myMap;

    function get(address _addr) public view returns (uint256) {
        // Mapping always returns a value.
        // If the value was never set, it will return the default value.
        return myMap[_addr];
    }

    function set(address _addr, uint256 _i) public {
        // Update the value at this address
        myMap[_addr] = _i;
    }

    function remove(address _addr) public {
        // Reset the value to the default value.
        delete myMap[_addr];
    }
}

contract NestedMapping {
    // Nested mapping (mapping from address to another mapping)
    mapping(address => mapping(uint256 => bool)) public nested;

    function get(address _addr1, uint256 _i) public view returns (bool) {
        // You can get values from a nested mapping
        // even when it is not initialized
        return nested[_addr1][_i];
    }

    function set(address _addr1, uint256 _i, bool _boo) public {
        nested[_addr1][_i] = _boo;
    }

    function remove(address _addr1, uint256 _i) public {
        delete nested[_addr1][_i];
    }
}

```
이 코드는 두개의 solidity 스마트 컨트랙트를 포함하고 있으며, 각각 `Mapping`과 `NestedMapping`이라는 이름을 갖고 있습니다.

#### Mapping 컨트랙트

이 컨트랙트는 주소(`address`)를 키로 하고, `uint256` 값을 저장하는 간단한 매핑을 사용합니다.

##### 변수
- `mapping(address => uint256) public myMap`: 주소를 키로 하고, `uint256` 값을 저장하는 공개 매핑입니다.

##### 함수
1. `get(address _addr) public view returns (uint256)`
- 입력된 주소 `_addr`에 대한 매핑 값을 반환합니다.
- 매핑 값이 설정되지 않은 경우, 디폴트 값인 `0`을 반환합니다.

```solidity
function get(address _addr) public view returns (uint256) {
    return myMap[_addr];
}
```

2. `set(address _addr, uint256 _i) public`
- 입력된 주소 `_addr`에 대해 값을 `_i`로 설정합니다.

```solidity
function set(address _addr, uint256 _i) public {
    myMap[_addr] = _i;
}
```

3. `remove(address _addr) public`
- 입력된 주소 `_addr`에 대한 매핑 값을 디폴트 값으로 재설정합니다. 이는 `delete`를 사용하여 이루어집니다.
```solidity
function remove(address _addr) public {
    delete myMap[_addr];
}
```

#### NestedMapping 컨트랙트

이 컨트랙트는 중첩 매핑을 사용하여 주소를 키로 하고, 그 주소에 대해 또 다른 매핑(정수형 `uint256`을 키로 하고 `boolean` 값을 저장하는 매핑)을 저장합니다.

##### 변수
- `mapping(address => mapping(uint256 => bool)) public nested`: 주소를 키로 하고, 또 다른 매핑(정수형을 키로 하고 불리언 값을 저장하는 매핑)을 값으로 갖는 중첩 매핑입니다.

#### 함수
1. `get(address _addr1, uint256 _i) public view returns (bool)`
- 입력된 주소 `_addr1`와 정수 `_i`에 대한 중첩 매핑 값을 반환합니다.
- 중첩 매핑 값이 설정되지 않은 경우, 디폴트 값인 `false`를 반환합니다.

```solidity
function get(address _addr1, uint256 _i) public view returns (bool) {
    return nested[_addr1][_i];
}
```

2. `set(address _addr1, uint256 _i, bool _boo) public`
- 입려된 주소 `_addr1`와 정수 `_i`에 대해 불리언 값을 `_boo`로 설정합니다.
```solidity
function set(address _addr1, uint256 _i, bool _boo) public {
    nested[_addr1][_i] = _boo;
}
```

3. `remove(address _addr1, uint256 _i) public`
- 입력된 주소 `_addr1`와 정수 `_i`에 대한 중첩 매핑 값을 디폴트 값(`false`)으로 재설정합니다. 이는 `delete`를 사용하여 이루어집니다.

<br/>

Remix에서 실습
-----------
1. Remix에서 새로운 solidity 파일을 생성해서 예제 코드를 복사 붙여넣기 합니다.
2. 예제 코드를 compile 후 deploy(각 컨트랙트에 맞게 배포)합니다.
3. 아래 버튼들이 제대로 동작하는지 확인합니다.

#### Mapping 컨트랙트
1. 값 설정
- `set` 함수를 호출하여 특정 주소에 값을 설정합니다.
- 예를 들어, 주소 `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`에 `123`을 설정합니다.
  - `_addr`: `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`
  - `_i`: `123`
- `set` 버튼을 클릭하고 입력 값을 채운 후 트랜잭션을 보냅니다.

2. 값 조회
- `get` 함수를 호출하여 설정된 값을 조회합니다.
- 같은 주소 `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`를 입력합니다.
- `get` 버튼을 클릭하면 반환 값이 `123` 임을 확인할 수 있습니다.

<img src="/images/Mapping/mapping1.png" width=40% height=70%><img/>

3. 값 삭제
- `remove` 함수를 호출하여 설정된 값을 삭제합니다.
- 동일한 주소 `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`를 입력하고 트랜잭션을 보냅니다.
- 다시 `get` 함수를 호출하여 값을 조회하면, 반환 값이 디폴트 값 `0` 임을 확인할 수 있습니다.

<img src="/images/Mapping/mapping2.png" width=40% height=70%><img/>

#### NestedMapping 컨트랙트
1. 값 설정
- `set` 함수를 호출하여 특정 주소와 키에 값을 설정합니다.
- 예를 들어, 주소 `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`와 키 `1`에 `true` 값을 설정합니다.
  - `_addr1`: `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`
  - `_i`: `1`
  - `_boo`:`true`
- `set` 버튼을 클릭하고 입력 값을 채운 후 트랜잭션을 보냅니다.

2. 값 조회
- `get` 함수를 호출하여 설정된 값을 조회합니다.
- 같은 주소 `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`와 키 `1`을 입력합니다.
- `get` 버튼을 클릭하면 반환 값이 `true`임을 확인할 수 있습니다.

<img src="/images/Mapping/nestedmapping1.png" width=40% height=70%><img/>

3. 값 삭제
- `remove` 함수를 호출하여 설정된 값을 삭제합니다.
- 동일한 주소 `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`와 키 `1`을 입력하고 트랜잭션을 보냅니다.
- 다시 `get` 함수를 호출하여 값을 조회하면, 반환 값이 디폴트 값 `false`임을 확인할 수 있습니다.

<img src="/images/Mapping/nestedmapping2.png" width=40% height=70%><img/>








